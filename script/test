#!/usr/bin/env rugo

# Rugo test runner - runs Go tests and discovers all examples
use "str"
use "conv"
use "os"

# Examples that block (e.g. start a server) or need network/auth in CI.
skip = [
  "examples/web_example.rugo",
  "examples/remote_require.rugo",
  "examples/require_go_qt/main.rugo"
]

def should_skip(path, skip_list)
  for s in skip_list
    if s == path
      return true
    end
  end
  return false
end

# Build rugo first
puts "==============================="
puts "  Building rugo"
puts "==============================="
go build -o /tmp/rugo_runner .
puts "  OK"
puts ""

def run_example(path)
  puts "--- example: " + path
  output = try os.exec("/tmp/rugo_runner run " + path) or err
    puts "  FAILED: " + err
    ""
  end
  if output != ""
    puts output
  end
  puts ""
end

# Go tests
puts "==============================="
puts "  Running Go tests"
puts "==============================="
puts ""
puts os.exec("go test ./... -count=1")
puts ""

# Discover and run examples
puts "==============================="
puts "  Running examples"
puts "==============================="
puts ""

output = `find examples -name '*.rugo' -type f -not -path '*/slug/*' | sort`
files = str.split(str.trim(output), "\n")
ran = 0
skipped = 0

for file in files
  if should_skip(file, skip)
    puts "--- example: " + file + " (skipped)"
    puts ""
    skipped += 1
    next
  end
  run_example(file)
  ran += 1
end

# Cleanup
rm /tmp/rugo_runner

puts "============================================="
puts "  All done: " + conv.to_s(ran) + " examples ran, " + conv.to_s(skipped) + " skipped, " + conv.to_s(len(files)) + " discovered"
puts "============================================="
